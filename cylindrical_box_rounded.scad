// Cuboidal rounded box with screw top

//////////////////////////////////////////////////
// constants
//////////////////////////////////////////////////

capHeight = 10;
bodyHeight = 50;
bodyWidth = 50;
wall = 1;
rounding = bodyWidth/2;
threadPitch = 5;
threadTurns = 1;
fullCap = 0;
circularity = 50;
threadPrecision = circularity;
$fn = circularity;

drawCap = 1;
drawBody = 1;

//////////////////////////////////////////////////
// draw objects
//////////////////////////////////////////////////

if (drawCap) cap();
if (drawBody) translate([bodyWidth+wall,0,0]) body(height = bodyHeight);

//////////////////////////////////////////////////
// modules
//////////////////////////////////////////////////

module cap() {
	difference (){
		union () {
			translate ([0,0,capHeight]) trapezoidThread(length=threadTurns*threadPitch, pitch=threadPitch, pitchRadius = bodyWidth/2-threadPitch/4-wall, stepsPerTurn=threadPrecision);
			cylinder(r=bodyWidth/2,h=capHeight);
		}
		
		translate([0,0,-.01]) notches(r1=bodyWidth/2-threadPitch/2, r2=bodyWidth/2, h=capHeight+0.02);
		torus(r=bodyWidth/2);
		if(!fullCap) translate([0,0,wall]) cylinder(r=bodyWidth/2-2*wall-threadPitch/2,h=capHeight+(threadTurns+1)*threadPitch);
	}    
}

module torus(r=1){
	rotate_extrude(convexity = 10)
	translate([r, 0, 0])
	circle(r = 1);
}

module notches(r1=1, r2=2, h=1){
  pi = 3.1415926535897932384626433832795;
	r3 = (r2-r1)*1.5 + r1; //actual outer radius
	r4 = r3-r1; //notch radius
	number = floor(pi*r3/r4*.8);
	angle = 360/number;
	for(i = [0 : angle : 360]) {           
			rotate([0,0,i]) translate ([r3,0,0]) cylinder (r=r4,h=h,center=false);
	}
}

module body() {
	h1 = threadPitch*(threadTurns+.5); // thread height
	h2 = bodyHeight-(h1-wall); // height of straight body
	
	rotate([180,0,0]) translate([0,0,-bodyHeight]){
		// straight body
		translate([-bodyWidth/2,-bodyWidth/2,h1]) difference(){
			cubeRounded([bodyWidth,bodyWidth,h2-wall], r=rounding);
			translate ([wall,wall,-2*wall]) cubeRounded([bodyWidth-2*wall,bodyWidth-2*wall,h2], r=rounding-wall);
		}
		
		// slope body
		difference(){
			difference(){
				// outer body
				rotate([0,0,45]) translate ([0,0,-bodyWidth/2+h1]) cylinder (r1=0,r2=bodyWidth/2*sqrt(2),h=bodyWidth/2, $fn=4);
				// inner cut
				difference(){
					rotate([0,0,45]) translate ([0,0,-bodyWidth/2+h1+wall*sqrt(2)]) cylinder (r1=0,r2=bodyWidth/2*sqrt(2),h=bodyWidth/2, $fn=4);
					difference(){
						translate([-bodyWidth,-bodyWidth,0]) cube([bodyWidth*2,bodyWidth*2,h2]);
						translate([-bodyWidth/2+wall,-bodyWidth/2+wall,0]) cubeRounded([bodyWidth-2*wall,bodyWidth-2*wall,h2], r=rounding-wall);
					}
				}
			}
			//center hole
			translate ([0,0,-bodyWidth+0.001]) cylinder (r=bodyWidth/2,h=h1+bodyWidth+0.001);
			// rounding edges
			difference(){
				translate([-bodyWidth,-bodyWidth,0]) cube([bodyWidth*2,bodyWidth*2,h2]);
				translate([-bodyWidth/2,-bodyWidth/2,0]) cubeRounded([bodyWidth,bodyWidth,h2], r=rounding);
			}
			// cutting top
			translate([-bodyWidth/2,-bodyWidth/2,-bodyWidth]) cube([bodyWidth,bodyWidth,bodyWidth]);
		}

		// thread
		difference(){	
			translate ([0,0,0]) cylinder (r=bodyWidth/2,h=h1);
			body_thread(2*h1);
		}
	}
}

module cubeRounded(dim=[10,10,10], r=1){
	difference(){
		cube(dim);
		translate([-0.001,-0.001,-0.001]) cube([r+0.001,r+0.001,dim[2]+0.002]);
		translate([dim[0]+0.001-r,-0.001,-0.001]) cube([r+0.001,r+0.001,dim[2]+0.002]);
		translate([-0.001,dim[1]+0.001-r,-0.001]) cube([r+0.001,r+0.001,dim[2]+0.002]);
		translate([dim[0]+0.001-r,dim[1]+0.001-r,-0.001]) cube([r+0.001,r+0.001,dim[2]+0.002]);
	}
	translate([r,r,0]) cylinder(r=r, h=dim[2]);
	translate([dim[0]-r,r,0]) cylinder(r=r, h=dim[2]);
	translate([r,dim[1]-r,0]) cylinder(r=r, h=dim[2]);
	translate([dim[0]-r,dim[1]-r,0]) cylinder(r=r, h=dim[2]);
}

module body_thread(h = 1) {
	translate ([0,0,0]) trapezoidThreadNegativeSpace(length=h, pitch=threadPitch, pitchRadius = bodyWidth/2-threadPitch/4-wall, stepsPerTurn=threadPrecision);
}


//////////////////////////////////////////////////////////////////////






//////////////////////////////////////////////////
// constants
//////////////////////////////////////////////////

cap = 0;
body = 0;
//wall = 1; // thickness of walls, don't forget thickness of inner thread
//height = 30; // outter height of body
//innerDiameter = 30; // diameter of space inside the body
//capHeight = 10; // without thread


//////////////////////////////////////////////////
// draw objects
//////////////////////////////////////////////////

if (cap) cap();
if (body) translate ([(innerDiameter+3*wall+pitch)*sqrt(2),0,0]) body(height = height);

//////////////////////////////////////////////////
// modules
//////////////////////////////////////////////////




/*
* Thread library: http://www.thingiverse.com/thing:8793 by syvwlch
* Copied in to have one code file
*/

module threadPiece(Xa, Ya, Za, Xb, Yb, Zb, radiusa, radiusb, tipRatioa, tipRatiob, threadAngleTop, threadAngleBottom)
{	
	angleZ=atan2(Ya, Xa);
	twistZ=atan2(Yb, Xb)-atan2(Ya, Xa);

	polyPoints=[
		[Xa+ radiusa*cos(+angleZ),		Ya+ radiusa*sin(+angleZ),		Za ],
		[Xa+ radiusa*cos(+angleZ),		Ya+ radiusa*sin(+angleZ),		Za + radiusa*tipRatioa ],
		[Xa ,						Ya ,						Za+ radiusa*(tipRatioa+sin(threadAngleTop)) ],
		[Xa ,						Ya ,						Za ],
		[Xa ,						Ya ,						Za+ radiusa*sin(threadAngleBottom) ],
	
		[Xb+ radiusb*cos(angleZ+twistZ),	Yb+ radiusb*sin(angleZ+twistZ),	Zb ],
		[Xb+ radiusb*cos(angleZ+twistZ),	Yb+ radiusb*sin(angleZ+twistZ),	Zb+ radiusb*tipRatiob ],
		[Xb ,						Yb ,						Zb+ radiusb*(tipRatiob+sin(threadAngleTop)) ],
		[Xb ,						Yb ,						Zb ],
		[Xb ,						Yb ,						Zb+ radiusb*sin(threadAngleBottom)] ];
	
	polyTriangles=[
		[ 0, 1, 6 ], [ 0, 6, 5 ], 					// tip of profile
		[ 1, 7, 6 ], [ 1, 2, 7 ], 					// upper side of profile
		[ 0, 5, 4 ], [ 4, 5, 9 ], 					// lower side of profile
		[ 4, 9, 3 ], [ 9, 8, 3 ], [ 3, 8, 2 ], [ 8, 7, 2 ], 	// back of profile
		[ 0, 4, 3 ], [ 0, 3, 2 ], [ 0, 2, 1 ], 			// a side of profile
		[ 5, 8, 9 ], [ 5, 7, 8 ], [ 5, 6, 7 ]  			// b side of profile
		 ];
	
	polyhedron( polyPoints, polyTriangles );
}

module shaftPiece(Xa, Ya, Za, Xb, Yb, Zb, radiusa, radiusb, tipRatioa, tipRatiob, threadAngleTop, threadAngleBottom)
{	
	angleZ=atan2(Ya, Xa);
	twistZ=atan2(Yb, Xb)-atan2(Ya, Xa);

	threadAngleTop=15;
	threadAngleBottom=-15;

	shaftRatio=0.5;

	polyPoints1=[
		[Xa,						Ya,						Za + radiusa*sin(threadAngleBottom) ],
		[Xa,						Ya,						Za + radiusa*(tipRatioa+sin(threadAngleTop)) ],
		[Xa*shaftRatio,				Ya*shaftRatio ,				Za + radiusa*(tipRatioa+sin(threadAngleTop)) ],
		[Xa*shaftRatio ,				Ya*shaftRatio ,				Za ],
		[Xa*shaftRatio ,				Ya*shaftRatio ,				Za + radiusa*sin(threadAngleBottom) ],
	
		[Xb,						Yb,						Zb + radiusb*sin(threadAngleBottom) ],
		[Xb,						Yb,						Zb + radiusb*(tipRatiob+sin(threadAngleTop)) ],
		[Xb*shaftRatio ,				Yb*shaftRatio ,				Zb + radiusb*(tipRatiob+sin(threadAngleTop)) ],
		[Xb*shaftRatio ,				Yb*shaftRatio ,				Zb ],
		[Xb*shaftRatio ,				Yb*shaftRatio ,				Zb + radiusb*sin(threadAngleBottom) ] ];
	
	polyTriangles1=[
		[ 0, 1, 6 ], [ 0, 6, 5 ], 					// tip of profile
		[ 1, 7, 6 ], [ 1, 2, 7 ], 					// upper side of profile
		[ 0, 5, 4 ], [ 4, 5, 9 ], 					// lower side of profile
		[ 3, 4, 9 ], [ 9, 8, 3 ], [ 2, 3, 8 ], [ 8, 7, 2 ], 	// back of profile
		[ 0, 4, 3 ], [ 0, 3, 2 ], [ 0, 2, 1 ], 			// a side of profile
		[ 5, 8, 9 ], [ 5, 7, 8 ], [ 5, 6, 7 ]  			// b side of profile
		 ];
	

	// this is the back of the raised part of the profile
	polyhedron( polyPoints1, polyTriangles1 );
}

module trapezoidThread(
	length=45,				// axial length of the threaded rod
	pitch=10,				// axial distance from crest to crest
	pitchRadius=10,			// radial distance from center to mid-profile
	threadHeightToPitch=0.5,	// ratio between the height of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	profileRatio=0.5,			// ratio between the lengths of the raised part of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	threadAngle=30,			// angle between the two faces of the thread
						// std value for Acme is 29 or for metric lead screw is 30
	RH=true,				// true/false the thread winds clockwise looking along shaft, i.e.follows the Right Hand Rule
	clearance=0.1,			// radial clearance, normalized to thread height
	backlash=0.1,			// axial clearance, normalized to pitch
	stepsPerTurn=24			// number of slices to create per turn
		)
{

	numberTurns=length/pitch;
		
	steps=stepsPerTurn*numberTurns;

	trapezoidRatio=				2*profileRatio*(1-backlash);

	function	threadAngleTop(i)=	threadAngle/2;
	function	threadAngleBottom(i)=	-threadAngle/2;

	function 	threadHeight(i)=		pitch*threadHeightToPitch;

	function	pitchRadius(i)=		pitchRadius;
	function	minorRadius(i)=		pitchRadius(i)-0.5*threadHeight(i);
	
	function 	X(i)=				minorRadius(i)*cos(i*360*numberTurns);
	function 	Y(i)=				minorRadius(i)*sin(i*360*numberTurns);
	function 	Z(i)=				pitch*numberTurns*i;

	function	tip(i)=			trapezoidRatio*(1-0.5*sin(threadAngleTop(i))+0.5*sin(threadAngleBottom(i)));

	// this is the threaded rod

	if (RH==true)
	translate([0,0,-threadHeight(0)*sin(threadAngleBottom(0))])
	for (i=[0:steps-1])
	{
		threadPiece(
			Xa=				X(i/steps),
			Ya=				Y(i/steps),
			Za=				Z(i/steps),
			Xb=				X((i+1)/steps),
			Yb=				Y((i+1)/steps), 
			Zb=				Z((i+1)/steps), 
			radiusa=			threadHeight(i/steps), 
			radiusb=			threadHeight((i+1)/steps),
			tipRatioa=			tip(i/steps),
			tipRatiob=			tip((i+1)/steps),
			threadAngleTop=		threadAngleTop(i), 
			threadAngleBottom=	threadAngleBottom(i)
			);

		shaftPiece(
			Xa=				X(i/steps),
			Ya=				Y(i/steps),
			Za=				Z(i/steps),
			Xb=				X((i+1)/steps),
			Yb=				Y((i+1)/steps), 
			Zb=				Z((i+1)/steps), 
			radiusa=			threadHeight(i/steps), 
			radiusb=			threadHeight((i+1)/steps),
			tipRatioa=			tip(i/steps),
			tipRatiob=			tip((i+1)/steps),
			threadAngleTop=		threadAngleTop(i), 
			threadAngleBottom=	threadAngleBottom(i)
			);
	}

	if (RH==false)
	translate([0,0,-threadHeight(0)*sin(threadAngleBottom(0))])
	mirror([0,1,0])
	for (i=[0:steps-1])
	{
		threadPiece(
			Xa=				X(i/steps),
			Ya=				Y(i/steps),
			Za=				Z(i/steps),
			Xb=				X((i+1)/steps),
			Yb=				Y((i+1)/steps), 
			Zb=				Z((i+1)/steps), 
			radiusa=			threadHeight(i/steps), 
			radiusb=			threadHeight((i+1)/steps),
			tipRatioa=			tip(i/steps),
			tipRatiob=			tip((i+1)/steps),
			threadAngleTop=		threadAngleTop(i), 
			threadAngleBottom=	threadAngleBottom(i)
			);

		shaftPiece(
			Xa=				X(i/steps),
			Ya=				Y(i/steps),
			Za=				Z(i/steps),
			Xb=				X((i+1)/steps),
			Yb=				Y((i+1)/steps), 
			Zb=				Z((i+1)/steps), 
			radiusa=			threadHeight(i/steps), 
			radiusb=			threadHeight((i+1)/steps),
			tipRatioa=			tip(i/steps),
			tipRatiob=			tip((i+1)/steps),
			threadAngleTop=		threadAngleTop(i), 
			threadAngleBottom=	threadAngleBottom(i)
			);
	}

	rotate([0,0,180/stepsPerTurn])
	cylinder(
		h=length+threadHeight(1)*(tip(1)+sin( threadAngleTop(1) )-1*sin( threadAngleBottom(1) ) ),
		r1=minorRadius(0)-clearance*threadHeight(0),
		r2=minorRadius(0)-clearance*threadHeight(0)
			);
}

module trapezoidThreadNegativeSpace(
	length=45,				// axial length of the threaded rod
	pitch=10,				// axial distance from crest to crest
	pitchRadius=10,			// radial distance from center to mid-profile
	threadHeightToPitch=0.5,	// ratio between the height of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	profileRatio=0.5,			// ratio between the lengths of the raised part of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	threadAngle=30,			// angle between the two faces of the thread
						// std value for Acme is 29 or for metric lead screw is 30
	RH=true,				// true/false the thread winds clockwise looking along shaft, i.e.follows the Right Hand Rule
	countersunk=0,			// depth of 45 degree chamfered entries, normalized to pitch
	clearance=0.1,			// radial clearance, normalized to thread height
	backlash=0.1,			// axial clearance, normalized to pitch
	stepsPerTurn=24			// number of slices to create per turn
		)
{

	translate([0,0,-countersunk*pitch])	
	cylinder(
		h=2*countersunk*pitch, 
		r2=pitchRadius+clearance*pitch+0.25*pitch,
		r1=pitchRadius+clearance*pitch+0.25*pitch+2*countersunk*pitch
			);

	translate([0,0,countersunk*pitch])	
	translate([0,0,-pitch])
	trapezoidThread(
		length=length+0.5*pitch, 				// axial length of the threaded rod 
		pitch=pitch, 					// axial distance from crest to crest
		pitchRadius=pitchRadius+clearance*pitch, 	// radial distance from center to mid-profile
		threadHeightToPitch=threadHeightToPitch, 	// ratio between the height of the profile and the pitch 
									// std value for Acme or metric lead screw is 0.5
		profileRatio=profileRatio, 			// ratio between the lengths of the raised part of the profile and the pitch
									// std value for Acme or metric lead screw is 0.5
		threadAngle=threadAngle,			// angle between the two faces of the thread
									// std value for Acme is 29 or for metric lead screw is 30
		RH=true, 						// true/false the thread winds clockwise looking along shaft
									// i.e.follows  Right Hand Rule
		clearance=0, 					// radial clearance, normalized to thread height
		backlash=-backlash, 				// axial clearance, normalized to pitch
		stepsPerTurn=stepsPerTurn 			// number of slices to create per turn
			);	

	translate([0,0,length-countersunk*pitch])
	cylinder(
		h=2*countersunk*pitch, 
		r1=pitchRadius+clearance*pitch+0.25*pitch,
		r2=pitchRadius+clearance*pitch+0.25*pitch+2*countersunk*pitch
			);
}

module trapezoidNut(
	length=45,				// axial length of the threaded rod
	radius=25,				// outer radius of the nut
	pitch=10,				// axial distance from crest to crest
	pitchRadius=10,			// radial distance from center to mid-profile
	threadHeightToPitch=0.5,	// ratio between the height of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	profileRatio=0.5,			// ratio between the lengths of the raised part of the profile and the pitch
						// std value for Acme or metric lead screw is 0.5
	threadAngle=30,			// angle between the two faces of the thread
						// std value for Acme is 29 or for metric lead screw is 30
	RH=true,				// true/false the thread winds clockwise looking along shaft, i.e.follows the Right Hand Rule
	countersunk=0,			// depth of 45 degree chamfered entries, normalized to pitch
	clearance=0.1,			// radial clearance, normalized to thread height
	backlash=0.1,			// axial clearance, normalized to pitch
	stepsPerTurn=24			// number of slices to create per turn
		)
{
	difference() 
	{
		cylinder(
			h=length,
			r1=radius, 
			r2=radius
				);
		
		trapezoidThreadNegativeSpace(
			length=length, 					// axial length of the threaded rod 
			pitch=pitch, 					// axial distance from crest to crest
			pitchRadius=pitchRadius, 			// radial distance from center to mid-profile
			threadHeightToPitch=threadHeightToPitch, 	// ratio between the height of the profile and the pitch 
										// std value for Acme or metric lead screw is 0.5
			profileRatio=profileRatio, 			// ratio between the lengths of the raised part of the profile and the pitch
										// std value for Acme or metric lead screw is 0.5
			threadAngle=threadAngle,			// angle between the two faces of the thread
										// std value for Acme is 29 or for metric lead screw is 30
			RH=true, 						// true/false the thread winds clockwise looking along shaft
										// i.e.follows  Right Hand Rule
			countersunk=countersunk,			// depth of 45 degree countersunk entries, normalized to pitch
			clearance=clearance, 				// radial clearance, normalized to thread height
			backlash=backlash, 				// axial clearance, normalized to pitch
			stepsPerTurn=stepsPerTurn 			// number of slices to create per turn
				);	
	}
}